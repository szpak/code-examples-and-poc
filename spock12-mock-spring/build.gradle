buildscript {
    ext {
        springBootVersion = '2.1.0.M2'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'info.solidsoft.blog.spock12-mock-spring'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

ext.groovyVersion = "2.5.2"
ext.spockVersion = "1.2-RC1-groovy-2.5"

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-groovysh'
    }
    testCompile "org.spockframework:spock-spring:${spockVersion}"
    testCompile "info.solidsoft.spock:spock-global-unroll:0.5.1"
}


configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        //Spock 1.2-RC1 depends on Groovy 2.5.0 by default - https://github.com/spockframework/spock/pull/881
        if (details.requested.group == 'org.codehaus.groovy' && details.requested.name.startsWith('groovy-')) {
            if (details.requested.version != groovyVersion) {
                details.useVersion(groovyVersion)
            }
        }
    }
}
